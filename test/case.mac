defstruct(
  test_case(
    name,
    suite,
    case_context,
    failed_count,
    passed_count,
    inclusive_count))$

test_case_begin(suite,name,case_context):=
block(
  test_trace(sconcat("Begin test case:[",suite@name,"].","[",name,"]")),
  new(
    test_case(
      name:name,
      suite:suite,
      case_context:case_context,
      failed_count:0,
      passed_count:0,
      inclusive_count:0)))$

/*
the form like:
 test_run_case(case,[args])::=block(print(case),
   buildq([y:args],errcatch(splice(y))));
 can be passed.
*/
test_case_run(case,[args])::=buildq([y:args],errcatch(splice(y)));

test_case_end(case):=
block(
  test_trace("End test case:[",case@suite@name,"].","[",case@name,"]"),
  test_trace("Passed Assertions Count:",case@passed_count),
  test_trace("Inclusive Assertions Count:",case@inclusive_count),
  test_trace("Failed Assertions Count:",case@failed_count),
  if (case@inclusive_count > 0) then
  suite@inclusive_cases:append(suite@inclusive_cases,[case])
  else if (case@failed_count > 0) then
  suite@failed_cases:append(suite@failed_cases,[case])
  else
  suite@passed_cases:append(suite@passed_cases,[case]))$

test_case_assert_trace_compare(case,expected,actual):=
block(
  test_trace("[Actual]:"),
  test_trace(actual),
  test_trace("[Expected]:"),
  test_trace(expected))$

test_case_assert_trace_pass(case,message,expected,actual):=block(
  case@passed_count:case@passed_count + 1,
  test_trace("[Passed]:",message),
  test_case_assert_trace_compare(case,expected,actual))$
    
    test_case_assert_trace_failure(case,message,expected,actual):=block(
  case@failed_count:case@failed_count + 1,
  test_trace("[Failed]:",message),
  test_case_assert_trace_compare(case,expected,actual),
  error("Assertion Failed"))$

test_case_assert_inclusive(case,message):=
block(
  case@inclusive_count:case@inclusive_count + 1,
  test_trace("[Inclusive]:",message),
  error("Assertion Inclusive"))$

test_case_assert_equal(case,message,expected,[actual]):=block(
  maplist(
    lambda([a],
      if is(a=expected)
      then test_case_assert_trace_pass(case,message,expected,a)
      else test_case_assert_trace_failure(case,message,expected,a)),
    actual))$

test_case_assert_true(case,message,[actual]):=
maplist(
  lambda([a],
    if is(a)
    then test_case_assert_trace_pass(case,message,true,a)
    else test_case_assert_trace_failure(case,message,true,a)),
  actual)$

test_case_assert_false(case,message,[actual]):=
maplist(
  lambda([a],
    if is(not a)
    then test_case_assert_trace_pass(case,message,false,a)
    else test_case_assert_trace_failure(case,message,false,a)),
  actual)$

test_case_assert_not_equal(case,message,expected,[actual]):=block(
  maplist(
    lambda([a],
      if is(a#expected)
      then test_case_assert_trace_pass(case,message,expected,a)
      else test_case_assert_trace_failure(case,message,expected,a)),
    actual))$

test_case_log(case,[args])::=buildq([y:args],test_trace(splice(y)));